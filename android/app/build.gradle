apply plugin: 'com.android.application'

repositories {
    flatDir {
        //告诉gradle，编译时从指定的目录获取jar包
        dirs '../libs'
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.heasy.knowroute"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 2
        versionName "0.7.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias 'knowroute'
            keyPassword KEY_PASSWORD
            storeFile file('../knowroute.keystore')
            storePassword STORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            //so文件的存储目录
            jniLibs.srcDir 'libs'
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "knowroute-${defaultConfig.versionName}.apk"
                outputFileName = fileName
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation('com.android.support:appcompat-v7:28.0.0')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.android.support:multidex:1.0.3'
    implementation('com.howardreactive:rx:1.0.2') {
        exclude group: "com.android.support"
        exclude module: "support-v4"
    }
    implementation 'io.reactivex:rxjava:1.1.0'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation project(path: ':core')
    implementation project(path: ':map')
    implementation 'com.alibaba:fastjson:1.2.40'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation ('com.github.tony19:logback-android:1.1.1-7') {
        exclude group: "com.github.tony19"
        exclude module: "apktool-lib"
    }
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okio:okio:1.17.2'
    implementation ('dom4j:dom4j:1.6'){
        exclude group: "xml-apis"
        exclude module: "xml-apis"
    }
    implementation 'jaxen:jaxen:1.1.4'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation files('../libs/BaiduLBS_Android.jar')
    implementation(name: '../libs/DateTimePickerLibrary-0.4.8', ext: 'aar')
}
